000000******************************************************************
000100* Author: Henrique Salvadori Coelho
000200* henriquesc@gmail.com
000300* www.hcoelho.com
000400*
000500*     This application simulates a simple inventory control app in
000600* COBOL. It uses a .dat file to register data for products
000700* available in stock, their titles, prices and IDs. For every
000800* purchase recorded, the quantity available of the items is
000900* subtracted from the .dat file.
001000*     Due to the access being relative, it is not possible to
001100* edit the file in a normal text editor.
001200*     This application is not ment to be used in mainframes, since
001300* it requires input from the console. It should only be used as
001400* an example for producing COBOL applications.
001500*
001600* The application contains the following functionalities:
001700*    . Display products
001800*    . Search product by ID
001900*    . Insert new product
002000*    . Update product
002100*    . Remove product
002200*    . Display purchases receipt
002300*    . New purchase receipt
002400*    . Record purchase
002500******************************************************************
002600
002700*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-
002800 IDENTIFICATION DIVISION.
002900*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-
003000 PROGRAM-ID. TEST-PROGRAM.
003100
003200
003300*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-
003400 ENVIRONMENT DIVISION.
003500*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-
003600
003700 CONFIGURATION SECTION.
003800*-----------------------
003900
004000 INPUT-OUTPUT SECTION.
004100*-----------------------
004200 FILE-CONTROL.
004300     SELECT PRODUCTS-FILE ASSIGN TO "products.dat"
004400         FILE STATUS IS PRODUCTS-FILE-STATUS
004500         RELATIVE KEY IS PFR-ID
004600         ACCESS IS RANDOM
004700         ORGANIZATION IS RELATIVE.
004800
004900
005000*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-
005100 DATA DIVISION.
005200*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-
005300
005400 FILE SECTION.
005500*-----------------------
005600 FD PRODUCTS-FILE.
005700     01 PF-RECORDS.
005800         05 PFR-ID    PIC 9(6).
005900         05 PFR-TITLE PIC X(40).
006000         05 PFR-PRICE PIC 99999V99.
006100         05 PFR-QIS   PIC 9(4).
006200
006300
006400 WORKING-STORAGE SECTION.
006500*-----------------------
006600
006700 01 PRODUCTS-FILE-STATUS-R  PIC X(2).
006800 01 PRODUCTS-FILE-EOF       PIC A(1).
006900 01 PRODUCTS-FILE-STATUS    PIC X(2).
007000 01 PRODUCTS-FILE-FOUND-KEY PIC X(1).
007100
007200 01 PRODUCTS-RECORD.
007300     05 PR-ID    PIC 9(6).
007400     05 PR-TITLE PIC X(40).
007500     05 PR-PRICE PIC 99999V99.
007600     05 PR-QIS   PIC 9(4).
007700
007800 01 PRODUCTS-RECORD-DISPLAY.
007900     05 PRD-ID     PIC 9(6).
008000     05 FILLER     PIC X(2) VALUE SPACES.
008100     05 PRD-TITLE  PIC X(40).
008200     05 FILLER     PIC X(2) VALUE SPACES.
008300     05 PRD-PRICE  PIC $ZZ,ZZ9.99.
008400     05 FILLER     PIC X(14) VALUE SPACES.
008500     05 PRD-QIS    PIC Z,ZZ9.
008600
008700 01 PURCHASES-DISPLAY-HEADER.
008800     05 FILLER PIC X(8)  VALUE "ID".
008900     05 FILLER PIC X(42) VALUE "PRODUCT TITLE".
009000     05 FILLER PIC X(12) VALUE "UNIT PRICE".
009100     05 FILLER PIC X(17) VALUE "QUANTITY".
009200
009300 01 PRODUCTS-RECORD-DISPLAY-HEADER.
009400     05 FILLER PIC X(8)  VALUE "ID".
009500     05 FILLER PIC X(42) VALUE "PRODUCT TITLE".
009600     05 FILLER PIC X(12) VALUE "UNIT PRICE".
009700     05 FILLER PIC X(17) VALUE "QUANTITY".
009800
009900 01 PRODUCTS-NUM.
010000     05 PRODUCTS-NUM-ENTRIES      PIC 9(7).
010100     05 PRODUCTS-NUM-PRODUCTS     PIC 9(7).
010200     05 PRODUCTS-NUM-TOTAL        PIC 9(7).
010300     05 PRODUCTS-NUM-DSP-ENTRIES  PIC ZZZ,ZZZ,ZZ9.
010400     05 PRODUCTS-NUM-DSP-PRODUCTS PIC ZZZ,ZZZ,ZZ9.
010500     05 PRODUCTS-NUM-DSP-TOTAL    PIC $ZZZ,ZZ9V99.
010600
010700 01 USER-MENU-SELECTION PIC 9.
010800
010900 01 USER-PRODUCT-ID     PIC 9(6).
011000 01 USER-PRODUCT-FOUND  PIC x(1).
011100
011200 01 USER-PROMPT-CONFIRM PIC X(1).
011300
011400 01 TEMPORARY-PRODUCT-ID PIC 9(6).
011500
011600 01 PROMPT-UPDATE.
011700     05 PU-CHANGE-PRICE PIC X(1).
011800     05 PU-CHANGE-QIS   PIC X(1).
011900     05 PU-PRICE        PIC 99999V99.
012000     05 PU-QIS          PIC 9(4).
012100
012200 01 PURCHASES.
012300     05 P-INDEX           PIC 99 VALUE 1.
012400     05 P-AVAILABLE-INDEX PIC 99 VALUE 1.
012500     05 P-ID    PIC 9(6)     OCCURS 10 TIMES VALUE ZEROES.
012600     05 P-TITLE PIC X(40)    OCCURS 10 TIMES VALUE SPACES.
012700     05 P-PRICE PIC 99999V99 OCCURS 10 TIMES VALUE ZEROES.
012800     05 P-QTY   PIC 9(4)     OCCURS 10 TIMES VALUE ZEROES.
012900
013000
013100*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-
013200 PROCEDURE DIVISION.
013300*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-*-
013400
013500*-----------------------------------------------------------------
013600* 0000 - 0099   MAIN PROCEDURE
013700*-----------------------------------------------------------------
013800 0010-MAIN-PROCEDURE.
013900     PERFORM 1004-INITIALIZE-PURCHASES-ARRAY.
014000     PERFORM 0050-MAIN-MENU UNTIL USER-MENU-SELECTION = 9.
014100     PERFORM 9999-END-PROGRAM.
014200
014300*-----------------------------------------------------------------
014400 0050-MAIN-MENU.
014500     PERFORM 0060-MAIN-MENU-DISPLAY.
014600     IF USER-MENU-SELECTION = 1
014700         PERFORM 0110-DISPLAY-PRODUCTS
014800     ELSE IF USER-MENU-SELECTION = 2
014900         PERFORM 0210-PROMPT-PRODUCT
015000     ELSE IF USER-MENU-SELECTION = 3
015100         PERFORM 0310-PROMPT-PRODUCT
015200     ELSE IF USER-MENU-SELECTION = 4
015300         PERFORM 0410-UPDATE-PRODUCT
015400     ELSE IF USER-MENU-SELECTION = 5
015500         PERFORM 0510-REMOVE-PRODUCT
015600     ELSE IF USER-MENU-SELECTION = 6
015700         PERFORM 0610-DISPLAY-PURCHASES
015800     ELSE IF USER-MENU-SELECTION = 7
015900         PERFORM 0710-CLEAR-PURCHASES
016000     ELSE IF USER-MENU-SELECTION = 8
016100         PERFORM 0810-RECORD-PURCHASE
016200     ELSE IF USER-MENU-SELECTION = 9
016300         DISPLAY SPACE
016400     ELSE
016500         DISPLAY "Invalid selection: ", USER-MENU-SELECTION
016600     END-IF.
016700
016800*-----------------------------------------------------------------
016900 0060-MAIN-MENU-DISPLAY.
017000     PERFORM 2 TIMES
017100         DISPLAY SPACE
017200     END-PERFORM.
017300
017400     DISPLAY "Select the desired action".
017500     DISPLAY "1. Display products".
017600     DISPLAY "2. Search product by ID".
017700     DISPLAY "3. Insert new product".
017800     DISPLAY "4. Update product".
017900     DISPLAY "5. Remove product".
018000     DISPLAY "6. Display purchases receipt"
018100     DISPLAY "7. New purchase receipt"
018200     DISPLAY "8. Record purchase".
018300     DISPLAY "9. EXIT".
018400     DISPLAY "> " WITH NO ADVANCING.
018500
018600     ACCEPT USER-MENU-SELECTION.
018700
018800     PERFORM 2 TIMES
018900         DISPLAY SPACE
019000     END-PERFORM.
019100
019200
019300*-----------------------------------------------------------------
019400* 0100 - 0199   DISPLAY PRODUCTS
019500*-----------------------------------------------------------------
019600 0110-DISPLAY-PRODUCTS.
019700     MOVE 0 TO PRODUCTS-NUM-ENTRIES.
019800     MOVE 0 TO PRODUCTS-NUM-PRODUCTS.
019900
020000     OPEN INPUT PRODUCTS-FILE.
020100     PERFORM 9901-MASTER-FILE-CHECK.
020200     DISPLAY "DISPLAYING PRODUCTS".
020300     PERFORM 1005-PRINT-HORIZONTAL-BAR.
020400     DISPLAY PRODUCTS-RECORD-DISPLAY-HEADER.
020500     PERFORM 1005-PRINT-HORIZONTAL-BAR.
020600     PERFORM 0120-READ-PRODUCT-LINE UNTIL PRODUCTS-FILE-EOF = "Y".
020700     CLOSE PRODUCTS-FILE.
020800
020900     MOVE "N" TO PRODUCTS-FILE-EOF.
021000     MOVE PRODUCTS-NUM-ENTRIES  TO PRODUCTS-NUM-DSP-ENTRIES.
021100     MOVE PRODUCTS-NUM-PRODUCTS TO PRODUCTS-NUM-DSP-PRODUCTS.
021200
021300     PERFORM 1005-PRINT-HORIZONTAL-BAR.
021400     DISPLAY "TOTAL".
021500     DISPLAY PRODUCTS-NUM-DSP-ENTRIES, " entries".
021600     DISPLAY PRODUCTS-NUM-DSP-PRODUCTS, " products".
021700
021800*-----------------------------------------------------------------
021900 0120-READ-PRODUCT-LINE.
022000     READ PRODUCTS-FILE NEXT RECORD INTO PRODUCTS-RECORD
022100         AT END MOVE "Y" TO PRODUCTS-FILE-EOF
022200         NOT AT END PERFORM 0130-DISPLAY-PRODUCT-LINE
022300     END-READ.
022400
022500*-----------------------------------------------------------------
022600 0130-DISPLAY-PRODUCT-LINE.
022700     COMPUTE PRODUCTS-NUM-ENTRIES  = PRODUCTS-NUM-ENTRIES  + 1.
022800     COMPUTE PRODUCTS-NUM-PRODUCTS = PRODUCTS-NUM-PRODUCTS +
022900                                     PR-QIS.
023000     MOVE PR-ID    TO PRD-ID.
023100     MOVE PR-TITLE TO PRD-TITLE.
023200     MOVE PR-PRICE TO PRD-PRICE.
023300     MOVE PR-QIS   TO PRD-QIS.
023400     DISPLAY PRODUCTS-RECORD-DISPLAY.
023500
023600
023700*-----------------------------------------------------------------
023800* 0200 - 0299   SEARCHING PRODUCT BY ID
023900*-----------------------------------------------------------------
024000 0210-PROMPT-PRODUCT.
024100     DISPLAY "ID of the product to search: " WITH NO ADVANCING.
024200     ACCEPT USER-PRODUCT-ID.
024300     DISPLAY SPACE.
024400     PERFORM 0220-SEARCH-PRODUCT.
024500
024600*-----------------------------------------------------------------
024700 0220-SEARCH-PRODUCT.
024800     MOVE "N" TO USER-PRODUCT-FOUND.
024900     MOVE USER-PRODUCT-ID TO PFR-ID.
025000
025100     OPEN INPUT PRODUCTS-FILE.
025200     PERFORM 9901-MASTER-FILE-CHECK.
025300     DISPLAY "SEARCHING FOR PRODUCT ", USER-PRODUCT-ID.
025400     DISPLAY "-------------------".
025500     READ PRODUCTS-FILE INTO PRODUCTS-RECORD
025600         INVALID KEY DISPLAY "No products found with the id ",
025700             USER-PRODUCT-ID
025800         NOT INVALID KEY PERFORM
025900             PERFORM 0240-DISPLAY-PRODUCT-LINE
026000             MOVE USER-PRODUCT-ID TO PFR-ID
026100             MOVE "Y" TO USER-PRODUCT-FOUND
026200         END-PERFORM
026300     END-READ.
026400     CLOSE PRODUCTS-FILE.
026500
026600*-----------------------------------------------------------------
026700 0240-DISPLAY-PRODUCT-LINE.
026800     DISPLAY PRODUCTS-RECORD-DISPLAY-HEADER.
026900     PERFORM 1000-MOVE-PR-TO-PRD.
027000     DISPLAY PRODUCTS-RECORD-DISPLAY.
027100
027200
027300*-----------------------------------------------------------------
027400* 0300 - 0399   INSERTING NEW PRODUCT
027500*-----------------------------------------------------------------
027600 0310-PROMPT-PRODUCT.
027700     INITIALIZE PRODUCTS-RECORD.
027800     DISPLAY "Product title (insert whitespaces) in the format:".
027900     DISPLAY "TTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTTT".
028000     ACCEPT PR-TITLE.
028100     DISPLAY "Product price: " WITH NO ADVANCING.
028200     ACCEPT PR-PRICE.
028300     DISPLAY "Quantity in stock: " WITH NO ADVANCING.
028400     ACCEPT PR-QIS.
028500     PERFORM 1001-GENERATE-NEW-KEY.
028600     MOVE TEMPORARY-PRODUCT-ID TO PR-ID.
028700     DISPLAY SPACE.
028800     PERFORM 1000-MOVE-PR-TO-PRD.
028900
029000     DISPLAY PRODUCTS-RECORD-DISPLAY-HEADER.
029100     DISPLAY PRODUCTS-RECORD-DISPLAY.
029200     DISPLAY "Do you wish to insert this product? (Y/N) "
029300             WITH NO ADVANCING
029400     PERFORM 1002-PROMPT-CONFIRM
029500
029600     IF USER-PROMPT-CONFIRM = "Y" THEN
029700         PERFORM 0320-APPEND-PRODUCT
029800     ELSE
029900         DISPLAY "Product not inserted."
030000     END-IF.
030100
030200*-----------------------------------------------------------------
030300 0320-APPEND-PRODUCT.
030400     OPEN I-O PRODUCTS-FILE.
030500     PERFORM 9901-MASTER-FILE-CHECK.
030600     WRITE PF-RECORDS FROM PRODUCTS-RECORD
030700         INVALID KEY PERFORM 9902-INVALID-KEY
030800     END-WRITE.
030900     DISPLAY "Product inserted."
031000     CLOSE PRODUCTS-FILE.
031100
031200
031300*-----------------------------------------------------------------
031400* 0400 - 0499   UPDATING PRODUCT
031500*-----------------------------------------------------------------
031600 0410-UPDATE-PRODUCT.
031700     MOVE "N" TO PU-CHANGE-PRICE.
031800     MOVE "N" TO PU-CHANGE-QIS.
031900
032000     PERFORM 0210-PROMPT-PRODUCT.
032100     DISPLAY SPACE.
032200
032300     IF USER-PRODUCT-FOUND = "Y" THEN
032400         PERFORM 0420-PROMPT-PRODUCT-PRICE
032500         PERFORM 0430-PROMPT-PRODUCT-QIS
032600         PERFORM 0440-REWRITE-PRODUCT
032700     END-IF.
032800
032900*-----------------------------------------------------------------
033000 0420-PROMPT-PRODUCT-PRICE.
033100     DISPLAY "Do you want to update the price? (Y/N) " WITH
033200             NO ADVANCING.
033300     PERFORM 1002-PROMPT-CONFIRM.
033400     MOVE USER-PROMPT-CONFIRM TO PU-CHANGE-PRICE.
033500
033600     IF PU-CHANGE-PRICE = "Y" THEN
033700         DISPLAY "New product price: $" WITH NO ADVANCING
033800         ACCEPT PU-PRICE
033900     END-IF.
034000
034100*-----------------------------------------------------------------
034200 0430-PROMPT-PRODUCT-QIS.
034300     DISPLAY "Do you want to update the quantity in stock? ",
034400             "(Y/N) " WITH NO ADVANCING.
034500     PERFORM 1002-PROMPT-CONFIRM.
034600     MOVE USER-PROMPT-CONFIRM TO PU-CHANGE-QIS.
034700
034800     IF PU-CHANGE-QIS = "Y" THEN
034900         DISPLAY "New quantity in stock: " WITH NO ADVANCING
035000         ACCEPT PU-QIS
035100     END-IF.
035200
035300*-----------------------------------------------------------------
035400 0440-REWRITE-PRODUCT.
035500     IF PU-CHANGE-QIS = "Y" OR PU-CHANGE-PRICE = "Y" THEN
035600         OPEN I-O PRODUCTS-FILE
035700         PERFORM 9901-MASTER-FILE-CHECK
035800         READ PRODUCTS-FILE INTO PF-RECORDS
035900             INVALID KEY PERFORM 9902-INVALID-KEY
036000         END-READ
036100
036200         MOVE USER-PRODUCT-ID TO PFR-ID
036300
036400         IF PU-CHANGE-QIS = "Y"
036500             MOVE PU-QIS TO PFR-QIS
036600         END-IF
036700         IF PU-CHANGE-PRICE = "Y"
036800             MOVE PU-PRICE TO PFR-PRICE
036900         END-IF
037000
037100         REWRITE PF-RECORDS
037200             INVALID KEY PERFORM 9902-INVALID-KEY
037300         END-REWRITE
037400
037500         DISPLAY "Product updated."
037600
037700         CLOSE PRODUCTS-FILE
037800     END-IF.
037900
038000
038100*-----------------------------------------------------------------
038200* 0500 - 0599   REMOVING PRODUCT
038300*-----------------------------------------------------------------
038400 0510-REMOVE-PRODUCT.
038500     PERFORM 0210-PROMPT-PRODUCT.
038600     DISPLAY SPACE.
038700
038800     IF USER-PRODUCT-FOUND = "Y" THEN
038900         DISPLAY "Do you wish to remove this product? (Y/N) "
039000             WITH NO ADVANCING
039100         PERFORM 1002-PROMPT-CONFIRM
039200
039300         IF USER-PROMPT-CONFIRM = "Y" THEN
039400
039500             OPEN I-O PRODUCTS-FILE
039600             PERFORM 9901-MASTER-FILE-CHECK
039700             MOVE USER-PRODUCT-ID TO PFR-ID
039800             DELETE PRODUCTS-FILE RECORD
039900             CLOSE PRODUCTS-FILE
040000
040100             DISPLAY "Product removed."
040200
040300         END-IF
040400     END-IF.
040500
040600*-----------------------------------------------------------------
040700* 0600 - 0699   PERFORMING PURCHASE
040800*-----------------------------------------------------------------
040900 0610-DISPLAY-PURCHASES.
041000     MOVE 1 TO P-INDEX.
041100     MOVE 0 TO PRODUCTS-NUM-ENTRIES.
041200     MOVE 0 TO PRODUCTS-NUM-PRODUCTS.
041300     MOVE 0 TO PRODUCTS-NUM-TOTAL.
041400
041500     DISPLAY "DISPLAYING PRODUCTS".
041600     PERFORM 1005-PRINT-HORIZONTAL-BAR.
041700     DISPLAY PURCHASES-DISPLAY-HEADER.
041800     PERFORM 1005-PRINT-HORIZONTAL-BAR.
041900
042000     PERFORM 0620-DISPLAY-PURCHASE-ARRAY.
042100
042200     MOVE PRODUCTS-NUM-ENTRIES  TO PRODUCTS-NUM-DSP-ENTRIES.
042300     MOVE PRODUCTS-NUM-PRODUCTS TO PRODUCTS-NUM-DSP-PRODUCTS.
042400     MOVE PRODUCTS-NUM-TOTAL    TO PRODUCTS-NUM-DSP-TOTAL.
042500
042600     PERFORM 1005-PRINT-HORIZONTAL-BAR.
042700     DISPLAY "TOTAL".
042800     DISPLAY PRODUCTS-NUM-DSP-ENTRIES, " entries".
042900     DISPLAY PRODUCTS-NUM-DSP-PRODUCTS, " products".
043000     DISPLAY PRODUCTS-NUM-DSP-TOTAL, " subtotal".
043100
043200*-----------------------------------------------------------------
043300 0620-DISPLAY-PURCHASE-ARRAY.
043400     PERFORM VARYING P-INDEX FROM 1 BY 1 UNTIL P-INDEX > 10
043500         IF P-TITLE (P-INDEX) NOT = SPACES THEN
043600             PERFORM 0630-DISPLAY-PURCHASE-LINE
043700         END-IF
043800     END-PERFORM.
043900
044000*-----------------------------------------------------------------
044100 0630-DISPLAY-PURCHASE-LINE.
044200     COMPUTE PRODUCTS-NUM-ENTRIES  = PRODUCTS-NUM-ENTRIES  + 1.
044300     COMPUTE PRODUCTS-NUM-PRODUCTS = PRODUCTS-NUM-PRODUCTS +
044400         P-QTY (P-INDEX).
044500     COMPUTE PRODUCTS-NUM-TOTAL = PRODUCTS-NUM-TOTAL +
044600         (P-QTY (P-INDEX) * P-PRICE (P-INDEX)).
044700
044800     PERFORM 1003-MOVE-PURCHASES-TO-PRD.
044900     DISPLAY PRODUCTS-RECORD-DISPLAY.
045000
045100
045200*-----------------------------------------------------------------
045300* 0700 - 0799   CLEARING PURCHASE
045400*-----------------------------------------------------------------
045500 0710-CLEAR-PURCHASES.
045600     DISPLAY "Do you want to start a new receipt? (Y/N) " WITH
045700             NO ADVANCING.
045800     PERFORM 1002-PROMPT-CONFIRM.
045900
046000     IF USER-PROMPT-CONFIRM = "Y" THEN
046100         PERFORM 1004-INITIALIZE-PURCHASES-ARRAY
046200         DISPLAY "Purchases cleared."
046300     END-IF.
046400
046500
046600*-----------------------------------------------------------------
046700* 0800 - 0899   RECORD PURCHASE
046800*-----------------------------------------------------------------
046900 0810-RECORD-PURCHASE.
047000     IF P-AVAILABLE-INDEX < 10 AND P-AVAILABLE-INDEX > 0 THEN
047100         PERFORM 0820-PROMPT-PRODUCT
047200         DISPLAY "Product added."
047300     ELSE
047400         DISPLAY "Purchase cart is full!"
047500     END-IF.
047600
047700*-----------------------------------------------------------------
047800 0820-PROMPT-PRODUCT.
047900     DISPLAY "ID of the product: " WITH NO ADVANCING.
048000     ACCEPT USER-PRODUCT-ID.
048100     DISPLAY SPACE.
048200     PERFORM 0220-SEARCH-PRODUCT.
048300     IF USER-PRODUCT-FOUND = "Y" THEN
048400         PERFORM 0830-PROMPT-QUANTITY
048500         PERFORM 0840-ADD-PRODUCT
048600     END-IF.
048700
048800*-----------------------------------------------------------------
048900 0830-PROMPT-QUANTITY.
049000     MOVE 0 TO P-QTY (P-AVAILABLE-INDEX).
049100     PERFORM UNTIL P-QTY (P-AVAILABLE-INDEX) > 0
049200         DISPLAY "Quantity (0 to ", PR-QIS WITH NO ADVANCING
049300         DISPLAY "): " WITH NO ADVANCING
049400         ACCEPT P-QTY (P-AVAILABLE-INDEX)
049500
049600         IF P-QTY (P-AVAILABLE-INDEX) < 1 OR
049700            P-QTY (P-AVAILABLE-INDEX) > PR-QIS THEN
049800
049900             DISPLAY "Quantity must be between 1 and ", PR-QIS
050000             MOVE 0 TO P-QTY (P-AVAILABLE-INDEX)
050100
050200         END-IF
050300     END-PERFORM.
050400
050500*-----------------------------------------------------------------
050600 0840-ADD-PRODUCT.
050700     MOVE PR-ID    TO P-ID    (P-AVAILABLE-INDEX).
050800     MOVE PR-TITLE TO P-TITLE (P-AVAILABLE-INDEX).
050900     MOVE PR-PRICE TO P-PRICE (P-AVAILABLE-INDEX).
051000
051100     PERFORM 0850-UPDATE-PRODUCT-QUANTITY.
051200     ADD 1 TO P-AVAILABLE-INDEX.
051300
051400*-----------------------------------------------------------------
051500 0850-UPDATE-PRODUCT-QUANTITY.
051600     OPEN I-O PRODUCTS-FILE.
051700     PERFORM 9901-MASTER-FILE-CHECK.
051800     MOVE PR-ID TO PFR-ID.
051900     READ PRODUCTS-FILE INTO PF-RECORDS
052000         INVALID KEY PERFORM 9902-INVALID-KEY
052100     END-READ.
052200
052300     COMPUTE PFR-QIS = PFR-QIS - P-QTY (P-AVAILABLE-INDEX).
052400
052500     REWRITE PF-RECORDS
052600         INVALID KEY PERFORM 9902-INVALID-KEY
052700     END-REWRITE.
052800
052900     CLOSE PRODUCTS-FILE.
053000
053100
053200*-----------------------------------------------------------------
053300* 1000 - 1999   MISC PROCEDURES
053400*-----------------------------------------------------------------
053500 1000-MOVE-PR-TO-PRD.
053600     MOVE PR-ID    TO PRD-ID.
053700     MOVE PR-TITLE TO PRD-TITLE.
053800     MOVE PR-PRICE TO PRD-PRICE.
053900     MOVE PR-QIS   TO PRD-QIS.
054000
054100*-----------------------------------------------------------------
054200 1001-GENERATE-NEW-KEY.
054300     MOVE 1 TO TEMPORARY-PRODUCT-ID.
054400     OPEN INPUT PRODUCTS-FILE.
054500     MOVE "N" TO PRODUCTS-FILE-FOUND-KEY.
054600     PERFORM UNTIL PRODUCTS-FILE-FOUND-KEY = "Y"
054700         MOVE TEMPORARY-PRODUCT-ID TO PFR-ID
054800         READ PRODUCTS-FILE
054900             INVALID KEY MOVE "Y" TO PRODUCTS-FILE-FOUND-KEY
055000             NOT INVALID KEY ADD 1 TO TEMPORARY-PRODUCT-ID
055100         END-READ
055200     END-PERFORM.
055300     CLOSE PRODUCTS-FILE.
055400
055500*-----------------------------------------------------------------
055600 1002-PROMPT-CONFIRM.
055700     ACCEPT USER-PROMPT-CONFIRM.
055800     IF USER-PROMPT-CONFIRM = "Y" OR
055900        USER-PROMPT-CONFIRM = "y" OR
056000        USER-PROMPT-CONFIRM = "1" THEN
056100         MOVE "Y" TO USER-PROMPT-CONFIRM
056200     ELSE
056300         MOVE "N" TO USER-PROMPT-CONFIRM
056400     END-IF.
056500
056600*-----------------------------------------------------------------
056700 1003-MOVE-PURCHASES-TO-PRD.
056800     MOVE P-ID    (P-INDEX) TO PRD-ID.
056900     MOVE P-TITLE (P-INDEX) TO PRD-TITLE.
057000     MOVE P-PRICE (P-INDEX) TO PRD-PRICE.
057100     MOVE P-QTY   (P-INDEX) TO PRD-QIS.
057200
057300*-----------------------------------------------------------------
057400 1004-INITIALIZE-PURCHASES-ARRAY.
057500     INITIALIZE PURCHASES.
057600     MOVE 1 TO P-AVAILABLE-INDEX.
057700
057800*-----------------------------------------------------------------
057900 1005-PRINT-HORIZONTAL-BAR.
058000     PERFORM 79 TIMES
058100         DISPLAY "-" WITH NO ADVANCING
058200     END-PERFORM.
058300     DISPLAY SPACE.
058400
058500*-----------------------------------------------------------------
058600* 9900 - 9999   ERRORS, ERROR CHECKS AND EXIT ROUTINES
058700*-----------------------------------------------------------------
058800 9901-MASTER-FILE-CHECK.
058900     IF PRODUCTS-FILE-STATUS NOT = "00" THEN
059000         DISPLAY "Error: Non 0 status returned from file open, ",
059100                 "error status is ", PRODUCTS-FILE-STATUS
059200         GO TO 9999-END-PROGRAM
059300     END-IF.
059400
059500*-----------------------------------------------------------------
059600 9902-INVALID-KEY.
059700     DISPLAY "Invalid key.".
059800     GO TO 9999-END-PROGRAM.
059900
060000
060100*-----------------------------------------------------------------
060200 9999-END-PROGRAM.
060300     STOP RUN.
060400     END PROGRAM TEST-PROGRAM.
060500
060600******************************************************************
